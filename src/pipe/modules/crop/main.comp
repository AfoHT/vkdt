#version 460
#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_nonuniform_qualifier    : enable

#include "shared.glsl"

layout(local_size_x = DT_LOCAL_SIZE_X, local_size_y = DT_LOCAL_SIZE_Y, local_size_z = 1) in;

// global uniform stuff about image and roi
layout(std140, set = 0, binding = 1) uniform params_t
{
  float h00, h01, h02, h10, h11, h12, h20, h21, h22;
  float crop_x, crop_X;
  float crop_y, crop_Y;
} params;


layout( // input f16 buffer rgb
    set = 1, binding = 0
) uniform sampler2D img_in;

layout( // output f16 buffer rgb
    set = 1, binding = 1
) uniform writeonly image2D img_out;

void
main()
{
  ivec2 ipos = ivec2(gl_GlobalInvocationID);
  if(any(greaterThanEqual(ipos, imageSize(img_out)))) return;

  vec2 co = vec2(params.crop_x, params.crop_y);
  vec2 xy = co * textureSize(img_in, 0) + vec2(ipos.xy);
  vec2 rd = vec2(
      (params.h00*xy.x + params.h01*xy.y + params.h02)/
      (params.h20*xy.x + params.h21*xy.y + params.h22),
      (params.h10*xy.x + params.h11*xy.y + params.h12)/
      (params.h20*xy.x + params.h21*xy.y + params.h22));
  // rounding texture access:
  // vec4 rgba = texelFetch(img_in, ivec2(rd+0.5), 0);
  // bilinear:
  // vec4 rgba = texture(img_in, rd/params.ri.roi);
  // catmull rom is a little slower (especially on intel) but results
  // in a bit more acuity:
  vec4 rgba = sample_catmull_rom(img_in, rd/textureSize(img_in, 0));
  imageStore(img_out, ipos, vec4(rgba.rgb, 1));
}

